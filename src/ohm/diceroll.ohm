Diceroll {

    Expr = ExprSumInfix

    DicerollToken = "d" | "D"

    ExprSumInfix
        = ExprSumInfix "+" ExprProductInfix --Add
        | ExprSumInfix "-" ExprProductInfix --Subtract
        | ExprProductInfix

    ExprProductInfix
        = ExprProductInfix "*" ExprPowerOfInfix --Multiply
        | ExprProductInfix "//" ExprPowerOfInfix --FloorDivide
        | ExprProductInfix "/" ExprPowerOfInfix --Divide
        | ExprPowerOfInfix

    ExprPowerOfInfix
        = ExprPowerOfInfix "^" ExprRollInfix --PowerOf
        | ExprRollInfix

    ExprRollInfix
        = ExprRollInfix DicerollToken ExprPriority --Dice
        | ExprPriority

    ExprPriority
        = "(" Expr ")" --Paren
        | DicerollToken ExprPriority --RollPrefix
        | "+" ExprPriority --PosPrefix
        | "-" ExprPriority --NegPrefix
        | Literal

    AttributeInner
        = variable_ident "(" Expr ")" --Function
        | variable_ident --Single
        
    Literal
        = "[input" digit "]" --FunctionInput
        | "[input]" --FunctionInputSingle
    	| "[" AttributeInner "]" --Attribute
        | number --number

    variable_ident
        = letter variable_ident_char*

    variable_ident_char
        = alnum | "_" | "-"

    number
        = digit* "." digit+ --fract
        | digit+ --whole
}